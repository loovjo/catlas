⍝ ⍵ = bytes of key
keyschedule ← {⎕IO←0
    rcon ← #.dec '00 01 02 04 08 10 20 40 80 1B 36'

    ~(≢⍵)∊ 16 24 32 : ('Invalid key size ', ⍕ ⍴⍵) ⎕SIGNAL 8

    N ← 4÷⍨≢⍵
    R ← (11 13 15)[2÷⍨N-4]

    W ← N 4 ⍴ ⍵

    ⍝ ⍵ = index
    ⍝ https://en.wikipedia.org/wiki/AES_key_schedule#The_key_schedule
    keyschedule_step ← {
        0=N|⍵ :         W[⍵-N ;] ##.xor (rcon[⍵÷N] 0 0 0) ##.xor sbox 1 ⊖ W[⍵-1 ;]
        (N=8) ∧ 4=N|⍵ : W[⍵-N ;] ##.xor sbox W[⍵-1 ;]
                        W[⍵-N ;] ##.xor W[⍵-1 ;]
    }

    _←{W ⊢← W⍪keyschedule_step ⍵}¨ N+⍳4×R-1

    R 16 ⍴ W
}

